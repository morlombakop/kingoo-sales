/*
 * Created by Mbakop Ngongang Morlo on 7/17/19 4:38 PM
 * Copyright (c) 2019 . All rights reserved.
 * Last modified 7/17/19 3:51 PM
 */

apply from: 'buildsystem/ci.gradle'
apply from: 'buildsystem/dependencies.gradle'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'se.patrikerdes.use-latest-versions'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

buildscript {
  ext.kotlin_version = '1.3.41'
  ext.gradle_tools = '3.4.2'
  ext.build_tools = '27.0.3'
  ext.check_updates_plugin = '0.21.0'
  ext.use_latest_versions = '0.2.11'
  ext.ktlint_plugin = '8.1.0'

  ext.compile_sdk = 29
  ext.target_sdk = 29
  ext.min_sdk = 21

  ext.application_id = 'com.morlombakop.kingoosales'
  ext.version_name = '1.0'
  ext.version_code = 1
  ext.test_runner = 'androidx.test.runner.AndroidJUnitRunner'


  repositories {
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
    google()
    jcenter()

  }
  dependencies {
    classpath "com.android.tools.build:gradle:$gradle_tools"
    //noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "se.patrikerdes:gradle-use-latest-versions-plugin:$use_latest_versions"
    classpath "com.github.ben-manes:gradle-versions-plugin:$check_updates_plugin"
    classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlint_plugin"
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

task runUnitTests(dependsOn: [':app:testDebugUnitTest']) {
  description 'Run all unit tests'
}

task runAcceptanceTests(dependsOn: [':app:connectedAndroidTest']) {
  description 'Run all acceptance tests.'
}

task deployDebug(type: Exec, dependsOn: 'app:installDebug') {
  def rootDir = project.rootDir
  def localProperties = new File(rootDir, "local.properties")
  if (localProperties.exists()) {
    Properties properties = new Properties()
    localProperties.withInputStream {
      inputStream -> properties.load(inputStream)
    }
    def sdkDir = properties.getProperty('sdk.dir')
    def adb = "$sdkDir/platform-tools/adb"
    commandLine "$adb", 'shell', 'am', 'start', '-n', 'com.morlombakop.kingoosales/com.morlombakop.kingoosales.core.navigation.RouteActivity'
  }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}
